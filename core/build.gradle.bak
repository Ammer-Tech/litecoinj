plugins {
    id 'java-library'
    id 'maven-publish'
    id 'eclipse'
}

version = '0.2'

dependencies {
    api 'org.bouncycastle:bcprov-jdk15to18:1.69'
    api 'com.google.guava:guava:31.0.1-android'
    api 'com.google.protobuf:protobuf-javalite:3.18.0'
    api 'com.squareup.okhttp3:okhttp:3.14.9'
    implementation 'com.lambdaworks:scrypt:1.4.0'
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'net.jcip:jcip-annotations:1.0'
    compileOnly 'org.fusesource.leveldbjni:leveldbjni-all:1.8'
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.easymock:easymock:3.2'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.5.2'
    testImplementation 'org.slf4j:slf4j-jdk14:1.7.32'
    testImplementation 'com.h2database:h2:1.3.167'
    testImplementation 'org.fusesource.leveldbjni:leveldbjni-all:1.8'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:2.5.2'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
}

sourceCompatibility = 11
targetCompatibility = 11
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

test {
    exclude 'org/litecoinj/core/PeerTest*'
    exclude 'org/litecoinj/core/TransactionBroadcastTest*'
    exclude 'org/litecoinj/net/discovery/DnsDiscoveryTest*'
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

ext.moduleName = 'org.litecoinj.core'

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes 'Automatic-Module-Name': moduleName
    }
}

publishing {
    repositories {
        maven {
            name = "Ammer-Tech"
            url = uri("https://maven.pkg.github.com/Ammer-Tech/publications")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'litecoinj-core'
            from components.java
            pom {
                description = 'A Java Litecoin library'
            }
        }
    }
}
